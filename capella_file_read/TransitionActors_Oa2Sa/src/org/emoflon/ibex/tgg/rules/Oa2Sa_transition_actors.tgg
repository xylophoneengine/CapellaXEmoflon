#using TransitionActors_Oa2Sa.*
#using AttrCondDefLibrary.*

#rule Oa2Sa_transition_actors #with TransitionActors_Oa2Sa

/**
 * This rule generates top-level an operational Entity (and its Part) and
 * transitions it to SystemAnalysis
 */

#source {
	src_oa_arch : oa.OperationalAnalysis{
		-ownedEntityPkg -> src_entity_pkg
	}
	
	src_entity_pkg : oa.EntityPkg{
		++ -ownedEntities->src_oa_entity
		++ -ownedParts -> src_oa_part
	}
	
	++ src_oa_entity : oa.Entity // id = 185

	++ src_oa_part : cs.Part{ // id = 165
		++ -abstractType-> src_oa_entity
	}
}

#target {
	trg_sa_arch : ctx.SystemAnalysis{
		-ownedSystemComponentPkg -> trg_componenet_pkg
	}
	
	trg_componenet_pkg : ctx.SystemComponentPkg{
		++ -ownedSystemComponents-> trg_sys_component
		++ -ownedParts -> trg_sys_part
	}
	
	++ trg_sys_component : ctx.SystemComponent
	
	++ trg_sys_part : cs.Part{
		++ -abstractType -> trg_sys_component
	}
}

#correspondence {
	oa2sa : Oa2Sa{
		#src-> src_oa_arch
		#trg-> trg_sa_arch
	}
	
	++ entity2component : Entity2SystemComponent{
		#src-> src_oa_entity
		#trg-> trg_sys_component
	}
}

#attributeConditions {
	eq_string(src_entity_pkg.name, "Operational Entities")
	eq_string(src_oa_entity.name, src_oa_part.name)
	
	
	eq_string(trg_componenet_pkg.name, "Structure")
	eq_string(trg_sys_component.name, trg_sys_part.name)
	
	eq_string(src_oa_entity.name, trg_sys_component.name)
}
